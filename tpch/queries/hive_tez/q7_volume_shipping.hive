set hive.execution.engine=tez;
DROP TABLE q7_volume_shipping;

-- create the target table
create table q7_volume_shipping (supp_nation string, cust_nation string, l_year int, revenue double) STORED AS ORC;

--set mapred.min.split.size=536870912;
--set hive.exec.reducers.bytes.per.reducer=1225000000;

-- the query
insert overwrite table q7_volume_shipping 
select
        supp_nation,
        cust_nation,
        l_year,
        sum(volume) as revenue
from
        (
                select
                        n1.n_name as supp_nation,
                        n2.n_name as cust_nation,
                        year(l_shipdate) as l_year,
                        l_extendedprice * (1 - l_discount) as volume
                from
                        supplier,
                        lineitem,
                        orders,
                        customer,
                        nation n1,
                        nation n2
                where
                        s_suppkey = l_suppkey
                        and o_orderkey = l_orderkey
                        and c_custkey = o_custkey
                        and s_nationkey = n1.n_nationkey
                        and c_nationkey = n2.n_nationkey
                        and (
                                (n1.n_name = 'INDIA' and n2.n_name = 'ETHIOPIA')
                                or (n1.n_name = 'ETHIOPIA' and n2.n_name = 'INDIA')
                        )
                        and l_shipdate >= '1995-01-01' and l_shipdate <= '1996-12-31'
        ) as shipping
group by
        supp_nation,
        cust_nation,
        l_year
order by
        supp_nation,
        cust_nation,
        l_year;
